; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Casino Statistics"
#define MyAppVersion "2.4"
#define MyAppPublisher "SET-Europe, UAB"
#define MyAppURL "http://set-europe.com"

[Setup]
AppId={{42114860-D9DE-4C53-AF9C-38A5D1715376}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputBaseFilename=statistics-setup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
WizardSmallImageFile=logo.bmp

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "statistics.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "statistics.jar"; DestDir: "{app}"; Flags: ignoreversion
Source: "statistics.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "postgresql-15.12-1-windows-x64.exe"; DestDir: "{tmp}";
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
; Add/Remove firewall exception
; Need to remove exceptions first when updating
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Casino Statistics (TCP:4567 IN)"" "; StatusMsg: "Removing Firewall Exception (TCP:4567 IN)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Casino Statistics (TCP:4567 OUT)"" "; StatusMsg: "Removing Firewall Exception (TCP:4567 OUT)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Casino Statistics (TCP:5432 IN)"" "; StatusMsg: "Removing Firewall Exception (TCP:5432 IN)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Casino Statistics (TCP:5432 OUT)"" "; StatusMsg: "Removing Firewall Exception (TCP:5432 OUT)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""Casino Statistics (TCP:4567 IN)"" action=allow protocol=TCP localport=4567 dir=in";copy; StatusMsg: "Adding Firewall Exception (TCP:4567 IN)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""Casino Statistics (TCP:4567 OUT)"" action=allow protocol=TCP localport=4567 dir=out"; StatusMsg: "Adding Firewall Exception (TCP:4567 OUT)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""Casino Statistics (TCP:5432 IN)"" action=allow protocol=TCP localport=5432 dir=in"; StatusMsg: "Adding Firewall Exception (TCP:5432 IN)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""Casino Statistics (TCP:5432 OUT)"" action=allow protocol=TCP localport=5432 dir=out"; StatusMsg: "Adding Firewall Exception (TCP:5432 OUT)"; Flags: runhidden
Filename: "{app}\statistics.exe"; Flags: runhidden; StatusMsg: Installing Service; Parameters: install
Filename: "{tmp}\postgresql-15.12-1-windows-x64.exe"; Flags: runminimized; StatusMsg: Installing PostgreSQL; Parameters: "--mode unattended --unattendedmodeui minimal --disable-components stackbuilder --superpassword postgres --servicepassword postgres --prefix ""{app}\PostgreSQL"" --datadir ""{app}\PostgreSQL\data"" --servicename casino-statistics-db"
Filename: "{app}\statistics.exe"; Flags: runhidden; StatusMsg: Starting Service; Parameters: start
Filename: "http://localhost:4567"; Flags: shellexec runasoriginaluser postinstall; Description: "Open Casino Statistics Web Service."

[UninstallRun]
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Casino Statistics (TCP:4567 IN)"" "; StatusMsg: "Removing Firewall Exception (TCP:4567 IN)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Casino Statistics (TCP:4567 OUT)"" "; StatusMsg: "Removing Firewall Exception (TCP:4567 OUT)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Casino Statistics (TCP:5432 IN)"" "; StatusMsg: "Removing Firewall Exception (TCP:5432 IN)"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Casino Statistics (TCP:5432 OUT)"" "; StatusMsg: "Removing Firewall Exception (TCP:5432 OUT)"; Flags: runhidden
Filename: "{app}\statistics.exe"; Flags: runhidden; Parameters: stop; RunOnceId: casino-statistics-stop
Filename: "{app}\statistics.exe"; Flags: runhidden; Parameters: uninstall; RunOnceId: casino-statistics-uninstall
Filename: "{app}\PostgreSQL\uninstall-postgresql.exe"; Flags: runminimized; Parameters: --mode unattended; RunOnceId: casino-statistics-db-uninstall

[Code]
var
  UserPage: TInputQueryWizardPage;
  database, username, password: String;

function ValidateInput(Sender: TWizardPage): Boolean;
begin
  Result := True;

  if Trim(UserPage.Values[0]) = '' then
  begin
    MsgBox('Database cannot be empty.', mbError, MB_OK);
    Result := False;
  end;

  if Trim(UserPage.Values[1]) = '' then
  begin
    MsgBox('Username cannot be empty.', mbError, MB_OK);
    Result := False;
  end;

  if Trim(UserPage.Values[2]) = '' then
  begin
    MsgBox('Password cannot be empty.', mbError, MB_OK);
    Result := False;
  end;
end;

procedure InitializeWizard;
var
  WarningLabel: TNewStaticText;
begin
  { Create the credentials input page }
  UserPage := CreateInputQueryPage(wpWelcome,
    'Casino Statistics Service', 'Credentials Setup',
    'Please specify username and password that will be used to access Casino Statistics');
  UserPage.Add('Database:', False);
  UserPage.Add('Username:', False);
  UserPage.Add('Password:', True);
  UserPage.Values[0] := 'stat';
  UserPage.Values[1] := 'statuser';
  UserPage.OnNextButtonClick := @ValidateInput;

  { Create a warning label on the Welcome page }
  WarningLabel := TNewStaticText.Create(WizardForm);
  WarningLabel.Parent := WizardForm.WelcomePage;
  WarningLabel.Caption := 'WARNING! Please ensure the installer is run with administrator privileges!';
  WarningLabel.Font.Color := clRed;
  WarningLabel.Font.Style := [fsBold];
  WarningLabel.Left := WizardForm.WelcomeLabel1.Left;
  WarningLabel.Top := WizardForm.WelcomeLabel1.Top + WizardForm.WelcomeLabel1.Height + ScaleY(20);
  WarningLabel.Width := WizardForm.WelcomeLabel1.Width;
  WarningLabel.AutoSize := True;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssInstall then begin
    database := UserPage.Values[0];
    username := UserPage.Values[1];
    password := UserPage.Values[2];
    ForceDirectories(ExpandConstant('{app}'));
    SaveStringToFile(ExpandConstant('{app}') + '\statistics.properties',
      'host = 127.0.0.1' + #13#10 +
      'database = ' + database + #13#10 +
      'user = ' + username + #13#10 +
      'pass = ' + password + #13#10 +
      'user1 = caixauser' + #13#10 +
      'pass1 = chipset' + #13#10,
      False);
  end;
end;